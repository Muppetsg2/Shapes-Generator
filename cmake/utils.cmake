# Get Shapes-Generator version from src/version.hpp and put it in SHAPES_GENERATOR_VERSION
function(shapes_generator_extract_version)
    file(READ "${CMAKE_CURRENT_LIST_DIR}/src/version.hpp" file_contents)
    string(REGEX MATCH "SHAPES_GENERATOR_VER_MAJOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract major version number from src/version.h")
    endif()
    set(ver_major ${CMAKE_MATCH_1})

    string(REGEX MATCH "SHAPES_GENERATOR_VER_MINOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract minor version number from src/version.h")
    endif()

    set(ver_minor ${CMAKE_MATCH_1})
    string(REGEX MATCH "SHAPES_GENERATOR_VER_PATCH ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract patch version number from src/version.h")
    endif()
    set(ver_patch ${CMAKE_MATCH_1})

    set(SHAPES_GENERATOR_VERSION_MAJOR ${ver_major} PARENT_SCOPE)
    set(SHAPES_GENERATOR_VERSION_MINOR ${ver_minor} PARENT_SCOPE)
    set(SHAPES_GENERATOR_VERSION_PATCH ${ver_patch} PARENT_SCOPE)
    set(SHAPES_GENERATOR_VERSION "${ver_major}.${ver_minor}.${ver_patch}" PARENT_SCOPE)
endfunction()